# Contains helper functions for a platform.
# For example, finding the installation location of Visual Studio

let fs = import('std/fs');
let os = import('std/os');

inline if os.name == 'windows' {

let vsWhereCmd = '"C:\\Program Files (x86)\\Microsoft Visual Studio\\Installer\\vswhere.exe" -latest -property installationPath';

let getVSInstallPath = fn() {
	let out = '';
	os.exec(vsWhereCmd, out);
	return out;
};

let getMSBuildPath = fn() {
	let vs = getVSInstallPath();
	if vs.empty() { return ''; }
	let msbuild = fs.pathFrom(vs, 'MSBuild', 'Current', 'Bin', 'msbuild.exe');
	if !fs.exists(msbuild) { return ''; }
	return msbuild;
};

}

# show a notification
let notify = fn(title, body = '') {
	title = '\'' + title + '\'';
	if !body.empty() { body = '\'' + body + '\''; }
	let cmd = '';
	if os.name == 'linux' {
		let notify = os.findExec('notify-send');
		if notify.empty() {
			raise('notification requires notify-send on Linux (could not find in $PATH)');
		}
		cmd = notify + ' -u normal -t 5000 ' + title + ' ' + body;
	} elif os.name == 'macos' {
		let notify = os.findExec('osascript');
		if notify.empty() {
			raise('notification requires osascript on macOS (could not find in $PATH)');
		}
		if body.empty() {
			cmd = notify + ' -e \'display notification ' + title + '\'';
		} else {
			cmd = notify + ' -e \'display notification ' + body + ' with title ' + title + '\'';
		}
	} elif os.name == 'android' {
		let notify = os.findExec('termux-notification');
		if notify.empty() {
			raise('notification requires termux-notification on Android (could not find in $PATH)');
		}
		cmd = notify + ' --title ' + title;
		if !body.empty() {
			cmd += ' --content ' + body;
		}
	} else {
		raise('notifications unimplemented for this OS');
	}
	return os.exec(cmd);
};