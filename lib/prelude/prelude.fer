loadlib('prelude/Prelude');

# Important stuff for core types and the VM (formerly std/sys module)
# Setup as a global variable 'feral'

############################ Core Functions ###########################

"
  fn(code = 0) -> nil
Shuts down / exits the program with `code` as the exit/status code.
"
let exit = fn(code = 0) {
    return exitNative(code);
};

"
  fn(maxRecursion) -> nil
Sets the maximum recursion limit for `VirtualMachine::execute()` to be `maxRecursion`.
"
let setMaxRecursion = fn(size = DEFAULT_MAX_RECURSION) {
    setMaxRecursionNative(size);
};

################################ Types ################################

# Str

"
  var.fn(base = 10) -> Int
Converts the string `var` which must represent an int and returns it as an int type using the given `base`.
"
let int in StrTy = fn(base = 10) {
    return self.intNative(base);
};

"
  var.fn(startIndex = 0, count = -1) -> Str
Returns the substring from `startIndex` to `count` characters within string `var`.
If `count` is `-1`, includes all the remaining characters.
"
let sub in StrTy = fn(begin = 0, len = -1) {
    if len == -1 { len = self.len(); }
    return self.substrNative(begin, len);
};

"
  var.fn(delimiter = ' ', count = -1) -> Vec<Str>
Splits the string `var` using `delimiter`, upto `count` number of times, and returns the resulting list of strings.
If `count` is less than 0, it's considered unlimited.
"
let split in StrTy = fn(delim = ' ', maxDelimCount = -1) {
    return self.splitNative(delim, maxDelimCount);
};

"
  var.fn() -> Bool
Returns `true` if the string `var` is a valid ASCII alphabet character (a-z, A-Z).
"
let isAlpha in StrTy = fn() {
    if self.empty() { return false; }
    return (self >= 'a' && self <= 'z') || (self >= 'A' && self <= 'Z');
};

"
  var.fn() -> Bool
Returns `true` if the string `var` is a valid ASCII digit character (0-9).
"
let isDigit in StrTy = fn() {
    if self.empty() { return false; }
    return self >= '0' && self <= '9';
};

"
  var.fn() -> Bool
Returns `true` if the string `var` is a valid ASCII space/tab/newline character (` ` / `\\t` / `\\r` / `\\n`).
"
let isSpace in StrTy = fn() {
    if self.empty() { return false; }
    return self == ' ' || self == '\t' || self == '\r' || self == '\n';
};

"
  var.fn(data...) -> var
Appends all the `data` in `var` using `.str()` member function on each item in `data` and returns `var` itself.
"
let append in StrTy = fn(data...) {
    for item in data.each() {
        self += item;
    }
    return ref(self);
};

"
  var.fn() -> Str
Returns the equivalent UTF-8 character from the given hex string `var`.
"
# requires the source to be a UTF-16 hex string.
let getUTF8CharFromHexString in StrTy = fn() {
    return self.getBinStrFromHexStr().getUTF8CharFromBinStr();
};

# Vec

"
  var.fn(other) -> Bool
Returns `true` if the vectors `var` and `other` have the same length and equal elements.
"
let '==' in VecTy = fn(other) {
    if self._type_() != other._type_() { return false; }
    if self.len() != other.len() { return false; }
    let len = self.len();
    for let i = 0; i < len; ++i {
        if self[i] != other[i] { return false; }
    }
    return true;
};

"
  var.fn(item) -> Bool
Returns `true` if the `item` is present in the vector `var`.
"
let find in VecTy = fn(obj) {
    for data in self.each() {
        if data == obj { return true; }
    }
    return false;
};

"
  var.fn(item) -> Bool
Returns `true` if the `item` is found in the vector `var` and is removed.
"
let remove in VecTy = fn(obj) {
    let len = self.len();
    for let i = 0; i < len; ++i {
        if self[i] == obj {
            self.erase(i);
            return true;
        }
    }
    return false;
};

"
  var.fn(with) -> Str
Creates and returns a `with` separated string of all the items in the vector `var`.
"
let join in VecTy = fn(with) {
    if self.empty() { return ''; }
    let res = '';
    let len = self.len();
    for item in self.each() {
        res += item.str();
        res += with;
    }
    let withLen = with.len();
    while withLen-- > 0 {
        res.pop();
    }
    return res;
};

"
  var.fn(list, startPos = 0, endPos = -1) -> var
Appends all items in `list` from `startPos` to `endPos` (exclusive) in the vector `var` and returns `var` itself.
If `endPos` is less than 0, it's considered to be end of the vector `list`.
"
let append in VecTy = fn(src, start = 0, end = -1) {
    return self.appendNative(src, start, end);
};

"
  var.fn(startPos, endPos = -1) -> Vec
Returns a vector that is a referenced subset from `startPos` to `endPos` (exclusive) of vector `var`.
If `endPos` is less than 0, it's considered to be end of the vector `var`.
"
let slice in VecTy = fn(start, end = -1) {
    if end == -1 { end = self.len(); }
    return self.sliceNative(start, end);
};

"
  var.fn(startPos, endPos = -1) -> Vec
Returns a vector that is a copy of the subset from `startPos` to `endPos` (exclusive) of vector `var`.
If `endPos` is less than 0, it's considered to be end of the vector `var`.
"
let sub in VecTy = fn(start, end = -1) {
    if end == -1 { end = self.len(); }
    return self.subNative(start, end);
};

"
  var.fn(comparator = nil) -> Nil
Sorts the given vector `var` and returns `var` itself.
A custom `comparator` may be optionally provided which must take 2 arguments and return `true` or `false` based on the sorting criteria.
Can also accept the following named arguments:
  `rev = true` to sort in the reverse order.
"
let sort in VecTy = fn(comparator = nil, .kw) {
    if self.len() <= 1 { return; }

    let rev = kw['rev'] ?? false;
    let cmp = comparator ?? (rev ? sortRevCmp : sortCmp);
    quickSort(self, cmp, 0, self.len() - 1);
};

"
  var.fn(comparator = nil) -> Var
Finds and returns the index of the smallest item in the vector `var`.
A custom `comparator` may be optionally provided which must take 2 arguments and return `true` or `false` based on the sorting criteria.
"
let min in VecTy = fn(lt = fn(a, b) { return a < b; }) {
    if self.empty() {
        raise('cannot find min in an empty list');
    }
    let count = self.len();
    let min = 0;
    for let i = 1; i < count; ++i {
        if lt(self[i], self[min]) { min = i; }
    }
    return min;
};

"
  var.fn(comparator = nil) -> Var
Finds and returns the index of the largest item in the vector `var`.
A custom `comparator` may be optionally provided which must take 2 arguments and return `true` or `false` based on the sorting criteria.
"
let max in VecTy = fn(gt = fn(a, b) { return a > b; }) {
    if self.empty() {
        raise('cannot find max in an empty list');
    }
    let count = self.len();
    let max = 0;
    for let i = 1; i < count; ++i {
        if gt(self[i], self[max]) { max = i; }
    }
    return max;
};

"
  var.fn() -> Str
Returns a string beginning with `[` and ending with `]`, containing a comma separated list of all the items in the vector `var`.
"
let str in VecTy = fn() {
    let res = '[';
    res += self.join(', ');
    res += ']';
    return res;
};

# Map

"
  var.fn(other) -> Bool
Returns `true` if the maps `var` and `other` have the same length and equal elements.
"
let '==' in MapTy = fn(other) {
    if self._type_() != other._type_() { return false; }
    if self.len() != other.len() { return false; }
    for e in self.each() {
        if other[e.0] != e.1 { return false; }
    }
    return true;
};

"
  var.fn() -> Str
Returns a string beginning with `{` and ending with `}`, containing a comma separated list of all the key-colon-value pairs in the map `var`.
"
let str in MapTy = fn() {
    let res = '{';
    for pair in self.each() {
        res += pair.0.str();
        res += ': ';
        res += pair.1.str();
        res += ', ';
    }
    if !self.empty() {
        res.pop();
        res.pop();
    }
    res += '}';
    return res;
};

"
  var.fn(fmtStr) -> Str
Uses the path `var` to format the format string `fmtStr` using specific shorthands, and returns the new string.
The shorthands are:
  $s => feral binary (which is running the program) location
  $p => `path` as is
  $d => parent directory of `path`
  $f => filename (file name without directory)
  $b => basename (file name without directory and extension)
  $e => file extension (including dot)
"
let fmt in PathTy = fn(fmtStr) {
    if fmtStr == nil { return nil; }
    let feralBinStr = feral.binaryPath.str();
    let selfStr = self.str();
    let shorthand = false;
    let parentStr = self.parent().str();
    let fileStr = self.file().str();
    let fileNameStr = self.fileName().str();
    let extStr = self.fileExt().str();
    let res = fmtStr;
    for let i = 0; i < res.len(); ++i {
        if res[i] == '$' && !shorthand {
            shorthand = true;
            res.erase(i);
            --i;
            continue;
        }
        if !shorthand { continue; }
        shorthand = false;
        if res[i] == 's' {
            res.erase(i);
            res.insert(i, feralBinStr);
            i += feralBinStr.len();
            --i;
            continue;
        }
        if res[i] == 'p' {
            res.erase(i);
            res.insert(i, selfStr);
            i += selfStr.len();
            --i;
            continue;
        }
        if res[i] == 'd' {
            res.erase(i);
            res.insert(i, parentStr);
            i += parentStr.len();
            --i;
            continue;
        }
        if res[i] == 'b' {
            res.erase(i);
            res.insert(i, fileNameStr);
            i += fileNameStr.len();
            --i;
            continue;
        }
        if res[i] == 'f' {
            res.erase(i);
            res.insert(i, fileStr);
            i += fileStr.len();
            --i;
            continue;
        }
        if res[i] == 'e' {
            res.erase(i);
            res.insert(i, extStr);
            i += extStr.len();
            --i;
            continue;
        }
    }
    return res;
};

# FS

"
  var.fn(path, mode = 'r') -> var
On an existing file handle `var`, closes it (if needed) and reopens using `path` and `mode` and returns `var` itself.
"
let reopen in FileTy = fn(file, mode = 'r') {
    return self.reopenNative(file, mode);
};

# Sorting helpers

let sortCmp = fn(a, b) { return a < b; };
let sortRevCmp = fn(a, b) { return a > b; };

let quickSort = fn(vec, cmp, low, high) {
    if low >= high { return; }
    let pi = quickSortPartition(vec, cmp, low, high);
    quickSort(vec, cmp, low, pi - 1);
    quickSort(vec, cmp, pi + 1, high);
};

let quickSortPartition = fn(vec, cmp, low, high) {
    let pivot = ref(vec[high]);
    let i = low - 1;
    for let j = low; j < high; ++j {
        if !cmp(vec[j], pivot) { continue; }
        ++i;
        vec.swap(i, j);
    }
    vec.swap(i + 1, high);
    return i + 1;
};