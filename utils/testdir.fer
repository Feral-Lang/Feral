#!/usr/bin/env feral

# executes all files in a given directory, depending on the config
# ensures they all exit with return code zero
# also performs cleanup as required

# config shorthands:
#   $s => feral binary location (self)
#   $p => full file path
#   $d => dirname (file's directory)
#   $f => filename (file name without directory)
#   $b => basename (file name without directory and extension)
#   $e => file extension

let io = import('std/io');
let fs = import('std/fs');
let os = import('std/os');
let vec = import('std/vec');
let fecl = import('std/fecl');
let stat = import('std/stat');
let time = import('std/time');
let mproc = import('std/multiproc');

let fmtWithPath = fn(path, fmtStrs) {
	if fmtStrs.empty() { return nil; }
	let v = vec.new(refs = true);
	for fmtStr in fmtStrs.each() {
		v.push(fs.fmtWithPath(fmtStr, path));
	}
	return v;
};

if feral.args.empty() {
	io.println('Usage: ', feral.binaryPath, ' ', __SRC_PATH__, ' <directory>');
	feral.exit(1);
}

let st = stat.stat(feral.args[0]) or err {
	io.println('error: stat failed on directory: ', err);
	feral.exit(1);
	return nil; # unreachable
};

if !st.isDir() {
	io.println('error: entered path is not a directory');
	feral.exit(1);
}

let cfgname = fs.pathFrom(feral.args[0], 'config.fecl');
if !fs.exists(cfgname) {
	io.println('error: test config: \'' + cfgname + '\' not found');
	feral.exit(1);
}
let cfgfile = fs.fopen(cfgname);
let cfgstr = '';
for line in cfgfile.eachLine() {
	cfgstr += line + '\n';
}
let cfg = fecl.loads(cfgstr);
let filePattern = cfg['FilePattern'];
let execCmd = cfg['ExecCmd'];
let cleanCmd = cfg['CleanCmd'];

let files = fs.walkDir(feral.args[0], fs.WALK_RECURSE, filePattern);
files.sort();
let passed = 0, failed = 0;
let tpool = vec.new(refs = true);
inline if os.name == 'windows' {
# Apparently, $null cannot be accessed by more than one process at a time.
let maxProcs = 1;
} else {
let maxProcs = mproc.getConcurrency();
}

let waitProcs = fn(maxProcs, withValgrind, counter = true) {
	while tpool.len() >= maxProcs {
		for let i = 0; i < tpool.len(); ++i {
			if !tpool[i].isDone() { continue; }
			let t = tpool[i];
			if t.getResult() != 0 {
				if counter { ++failed; }
				if withValgrind {
					io.cprintln('{r}failed {y}', files[t.getId()], '{c} with valgrind{0}, {y}code{0}: {r}', t.getResult(),'{0}');
				} else {
					io.cprintln('{r}failed {y}', files[t.getId()], '{0}, {y}code{0}: {r}', t.getResult(),'{0}');
				}
				io.fflush(io.stdout);
			} else {
				if counter { ++passed; }
			}
			tpool.erase(i--);
		}
		if tpool.len() >= maxProcs { os.sleep(1); } # 1 ms sleep
	}
};

let valgrindCmd = vec.new(refs = true);
if feral.args.find('--valgrind') {
	let valgrindExec = os.findExec('valgrind');
	if valgrindExec == nil {
		io.cprintln('could {r}not{0} find valgrind in {y}$PATH{0}');
		feral.exit(1);
	}
	# --log-fd=1 to pour the valgrind output on stdout so that we
	# don't have to redirect entire stderr to io.null
	valgrindCmd.push(valgrindExec);
	valgrindCmd.push('--fair-sched=yes');
	valgrindCmd.push('--log-fd=1');
	valgrindCmd.push('--leak-check=full');
	valgrindCmd.push('--show-leak-kinds=all');
}

let timeBegin = time.now();

for file in files.each() {
	waitProcs(maxProcs, !valgrindCmd.empty());
	let cmd = valgrindCmd;
	cmd.append(fmtWithPath(file, execCmd));
	cmd.push('^>' + io.null);
	if !valgrindCmd.empty() {
		io.cprintln('{c}test{0}({g}valgrind{0}): {y}', cmd.join(' '), '{0} ...');
	} else {
		io.cprintln('{c}test{0}: {y}', cmd.join(' '), '{0} ...');
	}
	io.fflush(io.stdout);
	tpool.push(mproc.run(cmd));
}
waitProcs(1, !valgrindCmd.empty());

let tot_time = time.now() - timeBegin;

if !cleanCmd.empty() {
	for file in files.each() {
		waitProcs(maxProcs, !valgrind.empty(), false);
		let cmd = fmtWithPath(file, cleanCmd);
		cmd.push('^>' + io.null);
		io.cprintln('{c}clean{0}: {y}', cmd.join(' '), '{0} ...');
		io.fflush(io.stdout);
		tpool.push(mproc.run(cmd));
	}
	waitProcs(1, !valgrind.empty(), false);
}

io.cprintln('total: {y}', files.len(),
	    '{0}, passed: {g}', passed,
	    '{0}, failed: {r}', failed,
	    '{0}, time: {b}', time.resolve(tot_time, time.milli).round(),
	    '{0} ms');
feral.exit(failed);