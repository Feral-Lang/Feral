let fmt = import('std/fmt');
let vec = import('std/vec');
let lang = import('std/lang');
let find_lib = import('./find_lib');

let shared_lib_base =
`# {self.lib_name}
set(mod "{self.lib_name}")
add_library($\\{mod} SHARED "$\\{PROJECT_SOURCE_DIR}/{self.src_path}/$\\{mod}.cpp" {self.other_srcs})
target_link_libraries($\\{mod}{dep_names} $\\{GMP_LIBRARY} $\\{GMPXX_LIBRARY} $\\{MPFR_LIBRARY})
set_target_properties($\\{mod}
    PROPERTIES
    PREFIX "libferal"
    LINK_FLAGS "$\\{EXTRA_LD_FLAGS}"
    LIBRARY_OUTPUT_DIRECTORY "$\\{CMAKE_BINARY_DIR}/lib/feral"
    INSTALL_RPATH_USE_LINK_PATH TRUE
)
install(TARGETS $\\{mod}
	LIBRARY
	  DESTINATION lib/feral
	  COMPONENT Libraries
)`;

let feralvm = find_lib.new('feralvm');
feralvm.make_reqd().add_path_suffix('feral');

let shared_lib_t = lang.struct(
	lib_name = 'sample_shared_lib',
	other_srcs = '',
	deps = vec.new(refs = true),
	src_path = 'src'
);

let new = fn(name) { return shared_lib_t(lib_name = name); };

let add_src in shared_lib_t = fn(src) { self.other_srcs += ' ' + src; return self; };

let add_dep in shared_lib_t = fn(dep) { self.deps.push(dep); return self; };

let gen in shared_lib_t = fn() {
	let dep_names = ' ';
	for d in self.deps.each() {
		dep_names += d.name() + ' ';
	}
	dep_names.pop();
	return fmt.template(shared_lib_base);
};

let name in shared_lib_t = fn() {
	return self.lib_name;
};