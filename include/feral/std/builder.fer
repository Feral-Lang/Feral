let io = import('std/io');
let os = import('std/os');
let fs = import('std/fs');
let map = import('std/map');
let sys = import('std/sys');
let lang = import('std/lang');

let default_compiler = 'g++';
let default_inc_dir = '-I/usr/include';
let default_lib_dir = '-L/usr/lib';
let shared_lib_out_ext = '.so';
let shared_lib_flag = '-shared';

if os.name == 'macos' || os.name == 'bsd' {
	default_compiler = 'clang++';
	default_inc_dir = '-I/usr/local/include';
	default_lib_dir = '-L/usr/local/lib';
}
default_lib_dir += ' -L' + sys.dll_core_load_loc;

if os.name == 'macos' {
	shared_lib_out_ext = '.dylib';
	shared_lib_flag = '-dynamiclib';
}

# -lgmp is required for mac at least
let builder_t = lang.struct(
	is_dll = false,
	compiler = default_compiler,
	compiler_opts = ' -std=c++11 ',
	inc_dirs = ' ' + default_inc_dir + ' ',
	lib_dirs = ' ' + default_lib_dir + ' ',
	lib_flags = ' -lferalvm -lgmp ',
	srcs = ' ',
	linker_flags = ' '
);

let new = fn() {
	return builder_t();
};

let make_dll in builder_t = fn() {
	self.compiler_opts += shared_lib_flag + ' -fPIC -rdynamic -Wl,-rpath,' + sys.dll_load_loc + ' ';
	self.is_dll = true;
	return self;
};

let add_comp_opts in builder_t = fn(opt) {
	self.compiler_opts += opt + ' ';
	return self;
};

let add_inc in builder_t = fn(inc_dir) {
	self.inc_dirs += inc_dir + ' ';
	return self;
};

let add_lib in builder_t = fn(lib_flag, lib_dir = '') {
	if !lib_dir.empty() {
		self.lib_dirs += lib_dir + ' ';
	}
	self.lib_flags += lib_flag + ' ';
	return self;
};

let add_src in builder_t = fn(src) {
	self.srcs += src + ' ';
	return self;
};

let build in builder_t = fn(output_file, dry_run = sys.args.find('dry')) {
	let compiler_loc = os.exec_exists(self.compiler);
	if compiler_loc.empty() {
		io.cprintln('{p}could not find compiler {r}', self.compiler, ' {p}in {c}$PATH{0}');
		return 1;
	}

	if !dry_run && !fs.exists('build') { os.mkdir('build'); }

	let out_file = output_file;
	if self.is_dll { out_file = 'libferal' + out_file + shared_lib_out_ext; }
	let cmd = compiler_loc + self.compiler_opts + self.inc_dirs + self.lib_dirs + self.lib_flags + self.srcs + '-o build/' + out_file;
	io.cprintln('Building ...');
	if dry_run {
		io.cprintln('{w}=> {c}', cmd, '{0}');
		return 0;
	}
	io.cprintln('{w}=> {c}build/', out_file, '{0}');
	return os.exec(cmd);
};

let install in builder_t = fn(.dirs, dry_run = sys.args.find('dry')) {
	let something_to_install = false;
	io.cprintln('Installing ...');

	let res = 0;

	if dirs['inc_dirs'] != nil {
		something_to_install = true;
		io.cprintln('{w}=> {c}', dirs['inc_dirs'], ' {0}-> {c}', sys.inc_load_loc, '/ {0}...');
		if !dry_run { res = os.copy(dirs['inc_dirs'], sys.inc_load_loc + '/'); }
		if res != 0 { return res; }
	}

	if dirs['lib_dirs'] != nil {
		something_to_install = true;
		io.cprintln('{w}=> {c}', dirs['lib_dirs'], ' {0}-> {c}', sys.dll_load_loc, '/ {0}...');
		if !dry_run { res = os.copy(dirs['lib_dirs'], sys.dll_load_loc + '/'); }
		if res != 0 { return res; }
	}

	if !dry_run && something_to_install {
		io.cprintln('Installation successful!');
	} else {
		io.cprintln('Nothing to install!');
	}

	return res;
};